apply plugin: 'java'
apply plugin: 'maven-publish'

def utf8 = 'UTF-8'
def java = '11'
def dist = 'dist'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.graalvm.sdk:graal-sdk:20.0.+'
	testImplementation 'junit:junit:+'
	testImplementation 'org.assertj:assertj-core:+'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:+'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:+'
}

compileJava {
	options.encoding = utf8
	sourceCompatibility = java
	targetCompatibility = java
}

test {
	useJUnitPlatform()
}

task checkout(type: Exec) {
	commandLine 'git', 'worktree', 'add', dist, 'gh-pages'
	enabled = !file(dist).exists()
}

javadoc {
	exclude 'elva/**'
	exclude 'qxsl/extra/**'
	destinationDir = file("$dist/doc")
	options.links("https://docs.oracle.com/en/java/javase/$java/docs/api/")
	options.header("<a href='http://nextzlog.github.io/qxsl'>QxSL API</a>")
	options.charSet = options.encoding = utf8
}

publishing.repositories.maven {
	url = "$dist/mvn"
}

publishing.publications {
	qxsl(MavenPublication) {
		from components.java
		groupId = 'qxsl'
		def ver = 'git describe'.execute().text.trim()
		version = ver?.split("-", 3).take(2).join(".")
	}
}

javadoc.dependsOn(checkout)
publish.dependsOn(checkout)
publish.mustRunAfter(checkout)
