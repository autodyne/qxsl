apply plugin: 'application'
apply plugin: 'maven-publish'

def utf8 = 'UTF-8'
def dist = 'dist'
def java = JavaVersion.VERSION_17
def main = 'qxsl.utils.ShellUtil'

application.mainClass.set(main)

repositories.mavenCentral()

dependencies {
	testImplementation 'org.assertj:assertj-core:4.0.+'
	testImplementation 'org.junit.platform:junit-platform-launcher:6.0.+'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:6.0.+'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:6.0.+'
}

compileJava {
	options.encoding = utf8
	sourceCompatibility = java
	targetCompatibility = java
}

compileTestJava {
	options.encoding = utf8
	sourceCompatibility = java
	targetCompatibility = java
}

test.useJUnitPlatform()

jar.manifest.attributes 'Main-Class': main
jar.from configurations.compileClasspath.collect {
	it.isDirectory()? it: zipTree(it)
}

javadoc {
	exclude 'elva/**'
	exclude 'gaas/**'
	destinationDir = file("$dist/docs")
	options.links("https://docs.oracle.com/en/java/javase/$java/docs/api/")
	options.header("<a href='https://github.com/autodyne/qxsl'>GitHub</a>")
	options.charSet = options.encoding = utf8
}

publishing.repositories.maven {
	url = "$dist/mvn"
}

publishing.publications {
	qxsl(MavenPublication) {
		from components.java
		groupId = 'qxsl'
		def ver = 'git describe'.execute().text.trim()
		version = ver?.split("-", 3).take(2).join(".")
	}
}

publish.dependsOn(build)
publish.mustRunAfter(build)
